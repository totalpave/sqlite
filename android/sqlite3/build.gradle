
plugins {
    id 'com.android.library'
    id 'maven-publish'
}

task wrapper(type: Wrapper) {
    gradleVersion = "7.6.1"
}

android {
    namespace 'com.totalpave.sqlite3'

    compileSdk 33
    ndkVersion '25.0.8775105'
    buildToolsVersion = '33.0.2'

    defaultConfig {
        aarMetadata {
            minCompileSdk 24
        }

        minSdk 24

        externalNativeBuild {
            cmake {
                version '3.22.1'
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }

    packagingOptions {
        excludes = [
            "**/libc++_shared.so"
        ]
    }
}

dependencies {
    runtimeOnly 'totalpave:libcxx:25.0.8775105'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'totalpave'
            artifactId = 'sqlite3'
            version = file('../../VERSION').text.trim()

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            url = 'https://archiva.totalpave.com/repository/public'
            credentials {
                username findProperty('totalpave.repository.user')
                password findProperty('totalpave.repository.password')
            }
        }
    }
}
