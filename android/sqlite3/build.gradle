
plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.totalpave.sqlite3'

    compileSdk 34
    ndkVersion '28.0.12674087'

    defaultConfig {
        aarMetadata {
            minCompileSdk 24
        }

        minSdk 24

        externalNativeBuild {
            cmake {
                version '3.31.0'
                arguments += [ "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON" ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }

    packagingOptions {
        excludes = [
            "**/libc++_shared.so"
        ]
    }
}

dependencies {
    runtimeOnly 'totalpave:libcxx:28.0.12674087'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'totalpave'
            artifactId = 'sqlite3'
            version = file('../../VERSION').text.trim()

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            url = 'https://archiva.totalpave.com/repository/public'
            credentials {
                username findProperty('totalpave.repository.user')
                password findProperty('totalpave.repository.password')
            }
        }
    }
}
